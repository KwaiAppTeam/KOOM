plugins {
    id 'com.android.library'
}
apply from: rootProject.file('gradle/common.gradle')
apply from: rootProject.file('gradle/mavenCentral-android-publish.gradle')

android {
    defaultConfig {
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                abiFilters 'arm64-v8a'
                cppFlags '-std=c++17', '-fno-exceptions', '-fno-rtti'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    cppFlags '-Wl,--gc-sections', '-fvisibility=hidden', '-flto'
                }
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
            version '3.18.1'
        }
    }

    flavorDimensions "stl_mode"
    productFlavors {
        StaticCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = 'static'
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_static"]
                }
            }
        }
        SharedCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = ''
            externalNativeBuild {
		// Because koom-native-leak hook memory allocator, and hook implementation using some apis(may invoke memory allocator) from 
		// libc++, then infinite recursive call lead stack overflow. so we always using "c++_static", and this increase package size,
		// you can dynamic delivery native-leak module using plugin technology
                cmake {
                    arguments = ["-DANDROID_STL=c++_static"]
                }
            }
        }
    }

    packagingOptions {
        exclude('lib/*/libc++_shared.so')
    }
}

dependencies {
    implementation deps.gson
    implementation deps.androidx.appcompat
    implementation deps.androidx.constraintlayout
    implementation deps.material
    implementation deps.kotlin.stdlib
    testImplementation deps.junit
    androidTestImplementation deps.androidx.test.ext
    androidTestImplementation deps.androidx.test.espresso
    implementation project(path: ':koom-monitor-base')
    implementation project(path: ':koom-common:kwai-android-base')
}

